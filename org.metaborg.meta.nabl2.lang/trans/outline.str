module outline

imports

  libspoofax/editor/outline
  
  nabl2/shared/pp

  signatures/nabl2/lang/-
  pp/nabl2/lang/-
  nabl2/lang/-
  pp

rules

  editor-outline:
    (_, _, ast, path, project-path) -> outline
    with outline := <outline> ast
 
  outline = custom-label-outline(to-outline-label, to-outline-node)

  to-outline-label = pp-rule-label

  to-outline-node:
    Signature(_) -> Node("signature",[])

  to-outline-node:
    Rules(rule*) -> Node("rules", children)
    with children := <collect-om(outline);concat> rule*

rules

  pp-rule-label = pp-NaBL2(prettyprint-rule-label)

  prettyprint-rule-label:
     FunDef(name, params, _) -> [ H([SOpt(HS(), "0")], [name', S(" << "), params', S(" >>")]) ]
    with name' := <pp-one-Z(prettyprint-NaBL2-RuleId) <+ pp-one-Z(prettyprint-completion-aux)> name
    with params' := <pp-H-list(prettyprint-NaBL2-CVar|",") <+ pp-one-Z(prettyprint-completion-aux)> params

  prettyprint-rule-label:
      _ -> [ H([SOpt(HS(), "0")], [rdef', S(" [[ "), pattern', S(" "), params', S(" "), type', S(" ]]")]) ]
    where ?CGenMatchRuleNoConstraint(rdef, pattern, params, type) + ?CGenMatchRule(rdef, pattern, params, type, _)
    with rdef' := <pp-one-Z(prettyprint-NaBL2-CGenRuleDef) <+ pp-one-Z(prettyprint-completion-aux)> rdef
    with pattern' := <pp-one-Z(prettyprint-NaBL2-Pattern) <+ pp-one-Z(prettyprint-completion-aux)> pattern
    with params' := <pp-one-Z(prettyprint-NaBL2-CGenParamsPattern) <+ pp-one-Z(prettyprint-completion-aux)> params
    with type' := <pp-one-Z(prettyprint-NaBL2-CGenTypePattern) <+ pp-one-Z(prettyprint-completion-aux)> type

  prettyprint-rule-label:
      _ -> [ H([SOpt(HS(), "0")], [S("init "), params', S(" "), type']) ]
    where ?CGenInitRule(params, type, _) + ?CGenInitRuleNoConstraint(params, type)
    with params' := <pp-one-Z(prettyprint-NaBL2-CGenParamsTerm) <+ pp-one-Z(prettyprint-completion-aux)> params
    with type' := <pp-one-Z(prettyprint-NaBL2-CGenTypeTerm) <+ pp-one-Z(prettyprint-completion-aux)> type
