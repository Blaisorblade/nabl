module nabl2/lang/generation/cgen/signatures/nameresolution

imports

  libstrc
  nabl2/shared/main
  nabl2/lang/generation/cgen/-
  signatures/nabl2/lang/signatures/-
  nabl2/lang/util/stratego

rules

  signature-to-str:
    NameResolution(_) -> []

  sections-to-nameresolution-str:
    section* -> [Rules([labelstr*, orderstr*, wfstr*])]
    with param* := <filter(?Signature(<id>));concat;
                    filter(?NameResolution(<id>));concat> section*
    with if label*@[_|_] := <filter(?Labels(<id>));concat> param* then
           labelterm* := <map(explode(injection-to-term))> label*;
           labelstr* := [ |[ nabl2--custom-labels = !~List(labelterm*) ]| ]
         else
           labelstr* := []
         end
    with if order*@[_|_] := <filter(?Order(<id>));concat;
                                    map(\ Lt(l1,l2) -> (l1,l2) \)> param* then
           orderterm* := <map(explode(injection-to-term))> order*;
           orderstr* := [ |[ nabl2--custom-label-order = !~List(orderterm*) ]| ]
         else
           orderstr* := []
         end
    with if wf := <getfirst(?WF(<id>))> param* then
           wfterm := <explode(injection-to-term)> wf;
           wfstr* := [ |[ nabl2--custom-labels-wf = !~wfterm  ]| ]
         else
           wfstr* := []
         end
 
  signature-to-str:
    Namespaces(_) -> []
