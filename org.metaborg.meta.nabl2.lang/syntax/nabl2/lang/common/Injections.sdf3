module nabl2/lang/common/Injections

imports

  nabl2/shared/common/CTerms
  nabl2/shared/common/Identifiers
  nabl2/shared/common/Resolution
  nabl2/shared/common/ScopeGraph
  nabl2/shared/common/Sets
  nabl2/shared/constraints/Base
  nabl2/lang/common/Terms

template options

  keyword -/- [a-zA-Z0-9\'\_]

context-free syntax

  Message.DefaultMessage =

  TermIndex.DefaultTermIndex =
  TermIndex.TermIndexFrom    = <@<Var>>

  OccurrenceIndex.DefaultOccurrenceIndex =
  OccurrenceIndex.OccurrenceIndexFrom    = <@<Var>>

  NewScopes.NewScopes = <new <{CVar " "}+>>

  LabelRE.Option   = <<LabelRE>?>
  LabelRE.NonEmpty = <<LabelRE>+>

  Constraint.CDeclType = [[Occurrence] : [CTerm] [ConstraintPriority] [Message]]

  Constraint.CGen  = [[CTerm] genOf [CTerm] [Message]]
  Constraint.CInst = [[CTerm] instOf [CTerm] [Message]]

syntax

  Constraint-CF.CSetEq = CSet-CF LAYOUT?-CF "seteq" SetProj-CF LAYOUT?-CF CSet-CF LAYOUT?-CF Message-CF

context-free priorities

    { LabelRE.Option LabelRE.NonEmpty } // aka, the same as LabelRE.Closure
  > LabelRE.Concat
  > LabelRE.And
  > LabelRE.Or

lexical syntax

  Keyword = "seteq" | "new"
