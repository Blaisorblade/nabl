module testsuites/api/util

imports

  nabl2/api
  nabl2/runtime/prelude/base
  signatures/nabl2/shared/constraints/-
  signatures/testsuites/api/-
  testsuites/assert
 
rules

  false-has-errors =
    nabl2-get-ast-analysis;
    nabl2-analysis-has-errors

rules

  get-ast-type-succeeds =
    ?AstWithType(x);
    a := <nabl2-get-ast-analysis>;
    <nabl2-get-ast-type(|a)> x

  get-ast-type-fails =
    ?AstWithoutType(x);
    a := <nabl2-get-ast-analysis>;
    <not(nabl2-get-ast-type(|a))> x

  get-ast-decls =
    (?AstWithNoDecls(x) + ?AstWithOneDecl(x) + ?AstWithTwoDecls(x));
    a := <nabl2-get-ast-analysis>;
    <nabl2-get-ast-decls(|a)> x;
    length

  get-ast-refs =
    (?AstWithNoRefs(x) + ?AstWithOneRef(x) + ?AstWithTwoRefs(x));
    a := <nabl2-get-ast-analysis>;
    <nabl2-get-ast-refs(|a)> x;
    length

rules

  get-all-decls =
    ?GetAllDecls(xs);
    a := <nabl2-get-ast-analysis>;
    nx := <length> xs;
    (s) := <nabl2-get-ast-params(|a)> xs;
    decls := <nabl2-get-all-decls(|a)> s;
    <length> decls

  get-direct-edges =
    ?GetDirectEdges(x1,x2);
    a := <nabl2-get-ast-analysis>;
    (s1) := <nabl2-get-ast-params(|a)> x1;
    (s2) := <nabl2-get-ast-params(|a)> x2;
    <assert-equals(nabl2-get-direct-edges(|a)|[(P(),s1)])> s2;
    <assert-equals(nabl2-get-direct-edges(|a)|[s1])> (s2,P());
    <assert-equals(nabl2-get-direct-edges-inv(|a)|[(P(),s2)])> s1;
    <assert-equals(nabl2-get-direct-edges-inv(|a)|[s2])> (s1,P())

  get-import-edges =
    ?GetImportEdges(xs,xi);
    a := <nabl2-get-ast-analysis>;
    (s) := <nabl2-get-ast-params(|a)> xs;
    r := <nabl2-mk-occurrence> xi;
    <assert-equals(nabl2-get-import-edges(|a)|[(I(),r)])> s;
    <assert-equals(nabl2-get-import-edges(|a)|[r])> (s,I());
    <assert-equals(nabl2-get-import-edges-inv(|a)|[(I(),s)])> r;
    <assert-equals(nabl2-get-import-edges-inv(|a)|[s])> (r,I())

  get-assoc-edges =
    ?GetAssocEdges(xs,xi);
    a := <nabl2-get-ast-analysis>;
    (s) := <nabl2-get-ast-params(|a)> xs;
    d := <nabl2-mk-occurrence> xi;
    <assert-equals(nabl2-get-assoc-edges(|a)|[(I(),s)])> d;
    <assert-equals(nabl2-get-assoc-edges(|a)|[s])> (d,I());
    <assert-equals(nabl2-get-assoc-edges-inv(|a)|[(I(),d)])> s;
    <assert-equals(nabl2-get-assoc-edges-inv(|a)|[d])> (s,I())
