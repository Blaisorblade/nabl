module nabl2/shared/common/Resolution

imports

  nabl2/shared/common/Identifiers
  nabl2/shared/common/ScopeGraph

template options

  keyword -/- [a-zA-Z0-9\'\_]

context-free syntax

  Label.Label = LabelId
 
  PathLabel   = Label
  PathLabel   = BuiltinPathLabel
  
  BuiltinPathLabel.P = "P"
  BuiltinPathLabel.I = "I"
 
  AnyLabel          = PathLabel
  AnyLabel          = BuiltinAnyLabel

  BuiltinAnyLabel.D = "D"

  Path = <[<{Step " "}*>]>

  Step.D = <<Scope> D <Occurrence>>
  Step.E = <<Scope> E(<PathLabel>)>
  Step.N = <<Scope> N(<PathLabel>,<Occurrence>,<Path>)>
 
context-free syntax

  LabelRE.Empty      = <0>
  LabelRE.Epsilon    = <e>
  LabelRE            = PathLabel
  LabelRE.Closure    = <<LabelRE>*>
  LabelRE.Concat     = <<LabelRE>.<LabelRE>>     {right}
  LabelRE.And        = <<LabelRE> & <LabelRE>>   {left}
  LabelRE.Or         = <<LabelRE> | <LabelRE>>   {left}
  LabelRE            = <(<LabelRE>)>             {bracket}

  LabelOrder.Lt      = [[AnyLabel] < [AnyLabel]]

context-free priorities

    LabelRE.Closure
  > LabelRE.Concat
  > LabelRE.And
  > LabelRE.Or

context-free restrictions

  BuiltinPathLabel -/- [a-zA-Z0-9\_\']
  BuiltinAnyLabel  -/- [a-zA-Z0-9\_\']
